
/*
 * Allows a user to lke a specified post.
 *
 * Notice, will associate the like with the currently authenticated user.
 */
.arguments
   id:long
.description:Allows a user to create a like for a specified post


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/id
auth.ticket.verify


/*
 * Needed below to prevent users from liking their own posts and comments.
 */
auth.ticket.get


/*
 * Connecting to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Sanity checking invocation to make sure users don't like their own posts and comments.
    */
   data.read
      database-type:mysql
      table:posts
      columns
         user
      where
         and
            id.eq:x:@.arguments/*/id
   if
      eq
         get-value:x:@data.read/*/*/user
         get-value:x:@auth.ticket.get
      .lambda
         response.status.set:401
         return-nodes
            result:You cannot like your own posts or comments


   /*
    * Executing create invocation, making sure we associate like with authenticated user.
    */
   auth.ticket.get
   data.create
      database-type:mysql
      table:likes
      return-id:bool:false
      values
         user:x:@auth.ticket.get
         post_id.eq:x:@.arguments/*/id


   /*
    * Returning result to caller.
    */
   unwrap:x:+/*
   return
      result:success
