
/*
 * Creates a new page in the system.
 */
.arguments
   url:string
   name:string
   content:string
.description:Creates a new page in the system


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/url
validators.mandatory:x:@.arguments/*/name
validators.mandatory:x:@.arguments/*/content
auth.ticket.verify:root, admin


/*
 * Connecting to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Parametrising invocation.
    */
   add:x:@data.connect/*/data.create/*/values
      get-nodes:x:@.arguments/*


   /*
    * Invoking create slot.
    */
   data.create
      database-type:mysql
      table:pages
      return-id:bool:false
      values


   /*
    * Returning results to caller.
    */
   unwrap:x:+/*
   return
      result:success
