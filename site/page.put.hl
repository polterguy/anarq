
/*
 * Updating an existing page in the system.
 */
.arguments
   url:string
   name:string
   content:string
.description:Updates an existing page in the system


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/url
auth.ticket.verify:root, admin


/*
 * Verifying user has not been blocked from site.
 */
auth.ticket.get
unwrap:x:+/*
signal:anarq.users.verify-access
   user:x:@auth.ticket.get


/*
 * Connecting to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Parametrising invocation.
    */
   add:x:@data.connect/*/data.update/*/values
      get-nodes:x:@.arguments/*/name
      get-nodes:x:@.arguments/*/content


   /*
    * Invoking update slot.
    */
   data.update
      database-type:mysql
      table:pages
      values
      where
         and
            url.eq:x:@.arguments/*/url


   /*
    * Returning results to caller.
    */
   unwrap:x:+/*
   return
      affected:x:@data.update
