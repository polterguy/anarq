
/*
 * Authenticates a user in the system, with a JWT token not having an expiration.
 */
.arguments
   username:string
   password:string
.description:Authenticates a user in the system, with a JWT token not having an expiration date


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/username
validators.mandatory:x:@.arguments/*/password


/*
 * Invokes [magic.auth.authenticate] that does the heavy lifting, and creates our JWT token,
 * making sure we return that token to caller.
 */
date.min
unwrap:x:+/*
signal:magic.auth.authenticate
   username:x:@.arguments/*/username
   password:x:@.arguments/*/password
   expires:x:@date.min


/*
 * Returns the authentication JWT ticket created above to caller.
 */
return-nodes:x:@signal/*
