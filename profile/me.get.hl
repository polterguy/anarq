
/*
 * Returns profile information related to currently authenticated user.
 */
.description:Returns profile information related to currently authenticated user


/*
 * Sanity checking invocation.
 */
auth.ticket.verify


/*
 * Needed to parametrise read invocation(s) further down.
 */
auth.ticket.get


/*
 * Connecting to database
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Invoking read slot.
    */
   data.read
      database-type:mysql
      table:user_details
      columns
         type
         content
      where
         and
            user.eq:x:@auth.ticket.get


   /*
    * Making sure user exists.
    */
   if
      not
         exists:x:@data.read/*/*
      .lambda
         response.status.set:404
         return-nodes
            result:No such user


   /*
    * Making sure we default email_notifications to true, unless explictly set to 'false' (string).
    */
   if
      not
         exists:x:@data.read/*/*/type/=email_notifications
      .lambda
         add:x:@data.read
            .
               .
                  type:email_notifications
                  content:bool:true
   else


      /*
       * If value exists at all, it should be boolean false.
       */
      set-value:x:@data.read/*/*/type/=email_notifications/./*/content
         .:bool:false


   /*
    * Transforming result of above invocation to key/value collection to reduce bandwith usage,
    * and create a more semantically correct response.
    */
   for-each:x:@data.read/*

      // Buffer node.
      .to-add
         .foo

      // Creating key/value pair for above buffer node.
      set-name:x:@.to-add/0
         get-value:x:@.dp/#/*/type
      set-value:x:@.to-add/0
         get-value:x:@.dp/#/*/content

      // Adding above buffer node to return invocation below.
      insert-before:x:@data.connect/*/return-nodes/*/roles
         get-nodes:x:@.to-add/0


   /*
    * Retrieving meta data from users table in magic database.
    */
   data.connect:magic
      data.read
         table:users
         columns
            locked
            created
         where
            and
               username.eq:x:@auth.ticket.get
      data.read
         table:users_roles
         columns
            role
         where
            and
               user.eq:x:@auth.ticket.get


   /*
    * Adding meta information for user retrieved from magic database to return value.
    */
   insert-before:x:./*/return-nodes/*/roles
      get-nodes:x:@data.connect/*/data.read/[0,1]/*/*


   /*
    * Transforming result of above invocation to key/value collection to reduce bandwith usage,
    * and create a more semantically correct response.
    */
   for-each:x:@data.connect/*/data.read/[1,2]/*

      // Buffer node.
      .to-add
         .

      // Creating key/value pair for above buffer node.
      set-value:x:@.to-add/0
         get-value:x:@.dp/#/*/role

      // Adding above buffer node to return invocation below.
      add:x:@data.connect/@data.connect/*/return-nodes/*/roles
         get-nodes:x:@.to-add/0


   /*
    * Returning object in its entirety to caller.
    */
   return-nodes
      roles
