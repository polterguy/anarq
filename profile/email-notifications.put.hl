
/*
 * Saves the user's email notifications preferences.
 */
.arguments
   notifications:bool
.description:Saves the user's email notifications preferences


/*
 * Sanity checking invocation.
 */
auth.ticket.verify


/*
 * Verifying user has not been blocked from site.
 */
auth.ticket.get
unwrap:x:+/*
signal:anarq.users.verify-access
   user:x:@auth.ticket.get


/*
 * Opening database connection.
 */
data.connect:[generic|anarq]


   /*
    * Deleting old email_notifications ID.
    */
   data.delete
      database-type:mysql
      table:user_details
      where
         and
            type.eq:email_notifications
            user.eq:x:@auth.ticket.get


   /*
    * Inserting new preferences, but only if it's false, since default value if ommitted is true.
    */
   if
      eq
         get-value:x:@.arguments/*/notifications
         .:bool:false
      .lambda


         /*
          * New PayPal ID was provided.
          */
         data.create
            database-type:mysql
            table:user_details
            values
               type:email_notifications
               user:x:@auth.ticket.get
               content:false


   /*
    * Returns result to caller.
    */
   return-nodes
      result:success
