
/*
 * Evaluates the given [sql] and returns results to caller.
 */
.arguments
   databaseType:string
   database:string
   sql:string
   batch:bool
.description:"Evaluates the given SQL given for your specified databaseType, and returns the result to caller."


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Checking to see if an explicit database was specified, at which point we
 * connect to that database explicitly, instead of using the default database.
 */
if
   and
      not
         eq
            get-value:x:@.arguments/*/database
            .:
      not
         eq
            get-value:x:@.arguments/*/database
            .
   .lambda


      /*
       * Explicit database was given.
       */
      set-value:x:../*/foo.connect
         get-value:x:@.arguments/*/database


/*
 * Making sure we connect to the correct database type.
 */
set-name:x:./*/foo.connect
   strings.concat
      get-value:x:@.arguments/*/databaseType
      .:.connect


/*
 * Creating our database connection.
 * Notice, at this point the below slot will have been changed by
 * the above logic.
 */
foo.connect


   /*
    * Checking if database type is "mssql" and [batch] is true, at which point
    * we execute the script as a "batch" type of script (allowing for "GO" statements).
    */
   if
      and
         eq
            get-value:x:@.arguments/*/batch
            .:bool:true
         eq
            get-value:x:@.arguments/*/databaseType
            .:mssql
      .lambda
         mssql.execute-batch:x:@.arguments/*/sql
         return-nodes:x:-/*

   else


      /*
       * Executing SQL, making sure we get slot name correct.
       */
      set-name:x:./*/foo.select
         strings.concat
            get-value:x:@.arguments/*/databaseType
            .:.select


      /*
       * Executing SQL and returning results to caller.
       */
      foo.select:x:@.arguments/*/sql
      return-nodes:x:-/*
