
/*
 * Changes the password of the currently authenticated user.
 *
 * Requires the user to be successfully authenticated, and an [.arguments]/[password]
 * being the new password to use for authenticated user.
 */
slots.create:magic.change-password


   /*
    * Retrieving currently authenticated user's username, and sanity checking to make sure
    * client actually is authenticated.
    */
   auth.ticket.get
   if
      or
         eq
            get-value:x:@auth.ticket.get
            .
         eq
            get-value:x:@auth.ticket.get
            .:
      .lambda
         throw:You have to be authenticated in order to change your password


   /*
    * Ensuring a password was actually provided.
    */
   if
      or
         eq
            get-value:x:@.arguments/*/password
            .:
         eq
            get-value:x:@.arguments/*/password
            .
      .lambda
         throw:You have to provide a new password


   /*
    * Hashing password.
    */
   crypto.password.hash:x:@.arguments/*/password


   /*
    * Opens up our database connection.
    */
   mssql.connect:[magic_auth]


      /*
       * Selects the user with the given username from our table.
       */
      mssql.execute:update users set password = @password where username = @username
         @password:x:@crypto.password.hash
         @username:x:@auth.ticket.get


      /*
       * Returning success to caller.
       */
      return-nodes
         status:success
