
/*
 * Returns users in the system ordered by karma.
 */
.description:Returns users in the system ordered by karma
.arguments
   limit:long
   offset:long


/*
 * Sanity checking invocation.
 */
validators.integer:x:@.arguments/*/limit
   min:1
   max:100


/*
 * Connecting to database
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Parametrising read invocation.
    */
   add:x:./*/data.read
      get-nodes:x:@.arguments/*


   /*
    * Invoking read slot, making sure we only count likes on posts
    * that are more recent than 30 days.
    */
   math.subtract
      date.now
      time
         days:30
   data.read
      database-type:mysql
      table:user_details
         as:u
         join:posts
            as:p
            type:left
            on
               and
                  u.user:p.user
         join:likes
            as:l
            type:left
            on
               and
                  l.post_id:p.id
      columns
         u.user
         count(distinct l.user, l.post_id)
            as:karma
      where
         and
            p.created.mt:x:@math.subtract
      group
         user
      order:karma
         direction:desc
      order:p.created
         direction:desc


   /*
    * Returning results to caller.
    */
   add:x:./*/return-nodes
      get-nodes:x:@data.read/*
   return-nodes
