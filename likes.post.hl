
/*
 * Allows a user to lke a specified post.
 */
.arguments
   post_id:long
.description:Allows a user to like the specified post


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/post_id


/*
 * Retrieving username for currently authenticated user.
 */
auth.ticket.get


/*
 * Further sanity checking invocation, making sure user is authenticated.
 */
if
   eq
      get-value:x:@auth.ticket.get
      .
   .lambda


      /*
       * Anonymous user tried to like a post.
       */
      throw:Access denied
         public:true
         status:401


/*
 * Connecting to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Parametrising invocation.
    */
   add:x:+/*/values
      get-nodes:x:@.arguments/*


   /*
    * Executing create invocation.
    */
   data.create
      database-type:mysql
      table:likes
      return-id:bool:false
      values
         user:x:@auth.ticket.get


   /*
    * Returning result to caller.
    */
   unwrap:x:+/*
   return
      result:success
