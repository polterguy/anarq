
/*
 * Updating an existing item according to the specified argument(s).
 */
.arguments
   id:long
   content:string
.description:Updates an existing post according to the specified arguments


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/id
validators.mandatory:x:@.arguments/*/content
auth.ticket.verify


/*
 * Connecting to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * We only allow users to update their own comments.
    */
   auth.ticket.get


   /*
    * Parametrising invocation.
    */
   add:x:./*/data.update/*/where/*/and
      get-nodes:x:@.arguments/*/id
   add:x:./*/data.update/*/values
      get-nodes:x:@.arguments/*/content


   /*
    * Invoking update slot.
    */
   data.update
      database-type:mysql
      table:posts
      values
      where
         and
            user:x:@auth.ticket.get


   /*
    * Returning results to caller.
    */
   unwrap:x:+/*
   return
      affected:x:@data.update
