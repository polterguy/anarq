
/*
 * Returns count of all comments matching the specified argument(s) to caller.
 */
.arguments
   parent:long
.description:Returns count of all comments matching the specified argument(s) to caller


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/parent


/*
 * Connecting to database
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Invoking read slot.
    */
   .path
   set-value:x:@.path
      strings.concat
         .:/
         get-value:x:@.arguments/*/parent
         .:/
         .:%


   /*
    * Checking if user is authenticated, and if not we only return public posts.
    */
   auth.ticket.get
   if
      eq
         get-value:x:@auth.ticket.get
         .
      .lambda


         /*
          * Making sure we only return public posts.
          */
         add:x:@data.connect/*/data.read/*/where/*/and
            .
               visibility.eq:public

   else


      /*
       * Making sure we only return public and protected posts.
       */
      add:x:@data.connect/*/data.read/*/where/*/and
         .
            or
               visibility.eq:public
               visibility.eq:protected


   /*
    * Invoking read slot.
    */
   data.read
      database-type:mysql
      table:posts
      columns
         count(*)
            as:count
      where
         and
            path.like:x:@.path


   /*
    * Returns result to caller.
    */
   return-nodes:x:@data.read/*/*
