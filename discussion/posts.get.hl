
/*
 * Returns all top level posts matching the specified argument(s) to caller.
 */
.arguments
   limit:long
   offset:long
   topic.eq:string
   user.eq:string
.description:Returns all posts matching the specified argument(s) to caller allowing caller to page results


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/limit
validators.integer:x:@.arguments/*/limit
   min:1
   max:100


/*
 * Connecting to database
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Parametrising invocation.
    */
   add:x:+/+/+/+/+/+/+
      get-nodes:x:@.arguments/*/limit
      get-nodes:x:@.arguments/*/offset
   remove-nodes:x:@.arguments/*/limit
   remove-nodes:x:@.arguments/*/offset
   add:x:+/*/where/*
      get-nodes:x:@.arguments/*


   /*
    * Invoking read slot.
    */
   data.read
      database-type:mysql
      table:posts
      columns
         id
         topic
         content
         created
         user
      where
         and
            path


   /*
    * Returns result to caller.
    */
   return-nodes:x:@data.read/*
