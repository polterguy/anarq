
/*
 * Updating an existing OP post according to the specified argument(s).
 */
.arguments
   id:long
   content:string
   visibility:string
.description:Updates an existing OP post according to the specified arguments


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/id
validators.enum:x:@.arguments/*/visibility
   .:public
   .:protected
unwrap:x:+/*
signal:anarq.html.safeguard
   content:x:@.arguments/*/content
auth.ticket.verify


/*
 * Connecting to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * We only allow users to update their own posts.
    */
   auth.ticket.get


   /*
    * Parametrising invocation.
    */
   add:x:@data.connect/*/data.update/*/values
      get-nodes:x:@.arguments/*/visibility
      get-nodes:x:@.arguments/*/content


   /*
    * Invoking update slot.
    */
   data.update
      database-type:mysql
      table:posts
      values
      where
         and
            id.eq:x:@.arguments/*/id
            user.eq:x:@auth.ticket.get


   /*
    * Returning results to caller.
    */
   unwrap:x:+/*
   return
      affected:x:@data.update
