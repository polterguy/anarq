
/*
 * Returns all top level posts matching the specified argument(s) to caller.
 */
.arguments
   limit:long
   offset:long
   topic:string
   username:string
   minutes:int
.description:Returns all top level posts matching the specified argument(s) to caller allowing caller to page results, sorting according to number of likes, allowing caller to extract posts not older than a specified amount of minutes


/*
 * Sanity checking invocation.
 */
validators.integer:x:@.arguments/*/limit
   min:1
   max:100


/*
 * Verifying user has not been blocked from site.
 */
auth.ticket.get
if
   not
      eq
         get-value:x:@auth.ticket.get
         .
   .lambda
      unwrap:x:+/*
      signal:anarq.users.verify-access
         user:x:@auth.ticket.get


/*
 * Connecting to database
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Parametrising invocation.
    */
   add:x:@data.connect/*/data.read
      get-nodes:x:@.arguments/*/limit
      get-nodes:x:@.arguments/*/offset


   /*
    * Checking if caller wants to filter according to username.
    */
   if
      exists:x:@.arguments/*/username
      .lambda

         /*
          * Username explicitly specified as filtering condition.
          */
         unwrap:x:+/*/*
         add:x:@data.connect/*/data.read/*/where/*/and
            .
               p.user.eq:x:@.arguments/*/username


   /*
    * Checking if caller wants to filter according to topic.
    */
   if
      exists:x:@.arguments/*/topic
      .lambda

         /*
          * Topic explicitly specified as filtering condition.
          */
         unwrap:x:+/*/*
         add:x:@data.connect/*/data.read/*/where/*/and
            .
               p.topic.eq:x:@.arguments/*/topic


   /*
    * Checking if user is authenticated and has confirmed his email,
    * and if not we only return public posts.
    */
   if
      auth.ticket.in-role:root, admin, moderator, guest
      .lambda


         /*
          * Making sure we return public and protected posts.
          */
         add:x:@data.connect/*/data.read/*/where/*/and
            .
               or
                  p.visibility.eq:public
                  p.visibility.eq:friends


         /*
          * Checking if caller should have access to moderated posts.
          */
         if
            auth.ticket.in-role:root, admin, moderator
            .lambda
               add:x:@data.connect/*/data.read/*/where/*/and/*/or
                  .
                     p.visibility.eq:moderated

   else


      /*
       * Making sure we only return public and protected posts.
       */
      add:x:@data.connect/*/data.read/*/where/*/and
         .
            p.visibility.eq:public


   /*
    * Checking if user only wants to see posts for the last n minutes.
    */
   if
      exists:x:@.arguments/*/minutes
      .lambda


         /*
          * User only want to see posts created during the last n minutes.
          */
         math.subtract
            date.now
            time
               minutes:x:@.arguments/*/minutes
         unwrap:x:+/*/*
         add:x:@data.connect/*/data.read/*/where/*/and
            .
               p.created.mt:x:@math.subtract


   /*
    * Invoking read slot.
    */
   data.read
      database-type:mysql
      table:posts
         as:p
         join:likes
            as:l
            type:left
            on
               and
                  p.id:l.post_id
      columns
         p.id
         p.topic
         p.created
         p.user
            as:user
         p.visibility
         p.content
            as:excerpt
         count(l.post_id)
            as:licks
      where
         and
            p.parent.eq
      group
         p.id
      order:licks
         direction:desc
      order:p.created
         direction:desc


   /*
    * Verifying above actually returned anything at all.
    */
   if
      not
         exists:x:@data.read/*/*
      .lambda
         return-value:"[]"


   /*
    * Looping through excerpts, trying to intelligently figuring
    * out what to use as excerpt for post.
    */
   for-each:x:@data.read/*/*/excerpt


      /*
       * Splitting away Open Graph parts.
       */
      strings.split:x:@.dp/#
         .:"<!-- End of Open Graph -->\r\n"
      if
         eq
            get-count:x:@strings.split/*
            .:int:2
         .lambda

            /*
             * Post has Open Graph declaration.
             */
            set-value:x:@.dp/#
               get-value:x:@strings.split/1
            set-value:x:@.dp/#
               strings.trim-start:x:@.dp/#


      /*
       * Splitting on CR/LF.
       */
      strings.split:x:@.dp/#
         .:"\n"
      set-value:x:@.dp/#
         get-value:x:@strings.split/0


      /*
       * Removing Markdown header declarations.
       */
      set-value:x:@.dp/#
         strings.trim:x:@.dp/#
            .:#


      /*
       * Removing trailing white space characters.
       */
      set-value:x:@.dp/#
         strings.trim:x:@.dp/#


      /*
       * Splitting on (.).
       */
      strings.split:x:@.dp/#
         .:"."
      set-value:x:@.dp/#
         get-value:x:@strings.split/0


      /*
       * Splitting on (<) to avoid including Social Graph declarations.
       */
      strings.split:x:@.dp/#
         .:<
      set-value:x:@.dp/#
         get-value:x:@strings.split/0


      /*
       * Verifying excerpt is not longer than 150 characters.
       */
      if
         mt
            strings.length:x:@.dp/#
            .:int:80
         .lambda


            /*
             * Only returning first 80 characters.
             */
            set-value:x:@.dp/#
               strings.concat
                  strings.substring:x:@strings.split/0
                     .:0
                     .:80
                  .:" ..."


   /*
    * Returns result to caller.
    */
   return-nodes:x:@data.read/*
