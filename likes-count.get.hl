
/*
 * Counts the number of likes belonging to a specific post.
 */
.arguments
   post_id:long
.description:Counts likes for an individual post


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/post_id


/*
 * Connects to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Notice, if post not public, we don't allow anonymous users to count likes.
    */
   auth.ticket.get
   if
      eq
         get-value:x:@auth.ticket.get
         .
      .lambda


         /*
          * User is not authenticated, hence making sure post is public
          * before we allow anonymous user to count its likes.
          */
         data.read
            database-type:mysql
            table:posts
            columns
               visibility
            where
               and
                  post_id:x:@.arguments/*/post_id
            limit:1
         if
            not
               eq
                  get-value:x:@data.read/*/*
                  .:public
            .lambda


               /*
                * Anonymous user tried to count likes for post that was not publicly visible.
                */
               throw:Access denied
                  public:true
                  status:401


   /*
    * Parametrising read invocation.
    */
   add:x:+/*/where/*
      get-nodes:x:@.arguments/*


   /*
    * Executing read invocation.
    */
   data.read
      database-type:mysql
      table:likes
      columns
         count(*)
            as:count
      where
         and
      limit:long:1


   /*
    * Returning results from above invocation.
    */
   return-nodes:x:-/*/*
