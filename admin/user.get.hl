
/*
 * Returns all meta information associated with a user in the system.
 */
.arguments
   user:string
.description:Returns all meta information associated with a user in the system, such as email address, and other fields provided during registration


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/user
auth.ticket.verify:root, admin, moderator


/*
 * Verifying user has not been blocked from site.
 */
auth.ticket.get
unwrap:x:+/*
signal:anarq.users.verify-access
   user:x:@auth.ticket.get


/*
 * Connecting to database
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Invoking read slot.
    */
   data.read
      database-type:mysql
      table:user_details
      columns
         type
         content
      where
         and
            user.eq:x:@.arguments/*/user


   /*
    * Making sure user exists.
    */
   if
      not
         exists:x:@data.read/*
      .lambda
         throw:User was not found
            status:404
            public:true


   /*
    * Transforming result of above invocation to key/value collection to reduce bandwith usage,
    * and create a more semantically correct response.
    */
   for-each:x:@data.read/*

      // Buffer node.
      .to-add
         .foo

      // Creating key/value pair for above buffer node.
      set-name:x:@.to-add/0
         get-value:x:@.dp/#/*/type
      set-value:x:@.to-add/0
         get-value:x:@.dp/#/*/content

      // Adding above buffer node to return invocation below.
      insert-before:x:@data.connect/*/return-nodes/*/roles
         get-nodes:x:@.to-add/0


   /*
    * Retrieving meta data from users table in magic database.
    */
   data.connect:magic
      data.read
         table:users
         columns
            locked
            created
         where
            and
               username.eq:x:@.arguments/*/user
      data.read
         table:users_roles
         columns
            role
         where
            and
               user.eq:x:@.arguments/*/user


   /*
    * Adding meta information for user retrieved from magic database to return value.
    */
   insert-before:x:./*/return-nodes/*/roles
      get-nodes:x:@data.connect/*/data.read/[0,1]/*/*


   /*
    * Transforming result of above invocation to key/value collection to reduce bandwith usage,
    * and create a more semantically correct response.
    */
   for-each:x:@data.connect/*/data.read/[1,2]/*

      // Buffer node.
      .to-add
         .

      // Creating key/value pair for above buffer node.
      set-value:x:@.to-add/0
         get-value:x:@.dp/#/*/role

      // Adding above buffer node to return invocation below.
      add:x:@data.connect/@data.connect/*/return-nodes/*/roles
         get-nodes:x:@.to-add/0


   /*
    * Returning object in its entirety to caller.
    */
   return-nodes
      roles
