
/*
 * Deletes the specified OP post from the system.
 */
.arguments
   id:long
.description:Deletes an OP post from your database


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/id
auth.ticket.verify:root, admin


/*
 * Verifying user has not been blocked from site.
 */
auth.ticket.get
unwrap:x:+/*
signal:anarq.users.verify-access
   user:x:@auth.ticket.get


/*
 * Connecting to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Invoking delete slot.
    */
   data.delete
      database-type:mysql
      table:posts
      where
         and
            id.eq:x:@.arguments/*/id
            parent.eq


   /*
    * Returning results of invocation to caller.
    */
   unwrap:x:+/*
   return
      affected:x:@data.delete
