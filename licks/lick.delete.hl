
/*
 * Deletes a like for the specified post for the currently authenticated user.
 *
 * Notice, we only allow for users to delete their own likes.
 */
.arguments
   id:long
.description:Allows an authenticated user to delete a previously created like for a post. Endpoint can only be invoked by user who created the like in the first place.


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/id
auth.ticket.verify


/*
 * Verifying user has not been blocked from site.
 */
auth.ticket.get
unwrap:x:+/*
signal:anarq.users.verify-access
   user:x:@auth.ticket.get


/*
 * Connecting to database.
 */
data.connect:[generic|anarq]
   database-type:mysql


   /*
    * Executing delete invocation, making sure we add user as part of criteria
    * for which like to delete, to avoind having users delete other user's likes.
    */
   data.delete
      database-type:mysql
      table:likes
      where
         and
            user:x:@auth.ticket.get
            post_id:x:@.arguments/*/id


   /*
    * Returning results from above invocation.
    */
   unwrap:x:+/*
   return
      affected:x:@data.delete
